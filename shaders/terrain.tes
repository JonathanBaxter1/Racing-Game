#version 430

#define heightMap textures[0]
#define normalMap textures[1]
#define colorMap textures[2]

layout (quads, equal_spacing, ccw) in;

uniform mat4 projection;
uniform mat4 view;

uniform sampler2D textures[8];

out vec3 coord;
out vec3 normal;
out vec3 color;

void main()
{
	float u = gl_TessCoord.x;
	float v = gl_TessCoord.y;

	vec4 p0 = gl_in[0].gl_Position;
	vec4 p1 = gl_in[1].gl_Position;
	vec4 p2 = gl_in[2].gl_Position;
	vec4 p3 = gl_in[3].gl_Position;

	vec4 pa = mix(p0, p1, u);
	vec4 pb = mix(p3, p2, u);
	vec4 worldPos = mix(pa, pb, v);


	vec2 mapCoord = worldPos.xz/4096.0;
	float height = texture(heightMap, mapCoord).x*274.0;
	worldPos.y = height;

	float waterHeight = 50.0;
	gl_ClipDistance[0] = height - waterHeight;

	normal = 2.0*(texture(normalMap, mapCoord).xzy - 0.5);
	coord = worldPos.xyz/30.0;
	color = texture(colorMap, mapCoord).rgb;
	gl_Position = projection*view*worldPos;
}
